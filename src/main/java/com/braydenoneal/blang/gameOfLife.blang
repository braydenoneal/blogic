gameOfLife;

step = 0;
size = 1;
w = 19;
l = 19;
y = 2;

fn main() {
    step = (step + 1) % size;

    if step != 0 {
        return 0;
    }

    xs = [];

    for x in range(w) {
        zs = [];

        for z in range(l) {
            directions = [[-1, -1], [-1, 0], [-1, 1], [0, -1], [0, 1], [1, -1], [1, 0], [1, 1]];
            n = 0;

            for d in directions {
                if getBlock((x + d[0]) % w, y, (z + d[1]) % l) == block("white_concrete") {
                    n += 1;
                }
            }

            live = 0;

            if n == 3 {
                live = 1;
            } elif n < 2 or n > 3 {
                live = -1;
            }

            zs.append(live);
        }

        xs.append(zs);
    }

    for x in range(w) {
        for z in range(l) {
            live = xs[x][z];

            if live == 1 {
                breakBlock(x, y, z, |block| { return true; });
                placeBlock(x, y, z, |item| { return item == item("white_concrete"); });
            } elif live == -1 {
                breakBlock(x, y, z, |block| { return true; });
                placeBlock(x, y, z, |item| { return item == item("black_concrete"); });
            }
        }
    }
}
