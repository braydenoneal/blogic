step = 0;
size = 4;
w = 18;
l = 18;

fn main() {
    step = (step + 1) % size;
    if step == 0 {
        y = 2;
        xs = [];

        for x in range(w) {
            zs = [];

            for z in range(l) {
                directions = [[-1, -1], [-1, 0], [-1, 1], [0, -1], [0, 1], [1, -1], [1, 0], [1, 1]];
                n = 0;

                for d in directions {
                    if !isAirBlock((x + d[0]) % w, y, (z + d[1]) % l) {
                        n += 1;
                    }
                }

                live = 0;

                if n == 3 {
                    live = 1;
                } elif n < 2 or n > 3 {
                    live = -1;
                }

                zs += live;
            }

            xs += [zs];
        }

        for x in range(w) {
            for z in range(l) {
                live = xs[x][z];

                if live == 1 {
                    placeBlock(x, y, z, "white_concrete");
                } elif live == -1 {
                    placeBlock(x, y, z, "air");
                }
            }
        }
    }
}
